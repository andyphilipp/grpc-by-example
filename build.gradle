apply plugin: 'java'
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'

group 'no.philipp'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8
def grpcVersion = "1.3.0"

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile "io.grpc:grpc-protobuf:$grpcVersion"
    compile "io.grpc:grpc-stub:$grpcVersion"
    compile "io.grpc:grpc-netty:$grpcVersion"
    compile "io.grpc:grpc-services:$grpcVersion"
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.1'
    }
}

// Protobuf Gradle plugin config
protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.0.2"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
    generatedFilesBaseDir = "$projectDir/gen"
}

// Delete generated files on clean
clean {
    delete protobuf.generatedFilesBaseDir
}

// Add generated files to IntelliJ
idea {
    module {
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java");
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/grpc");
    }
}